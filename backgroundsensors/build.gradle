plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant('release') {
            withJavadocJar()
            withSourcesJar()
        }

        singleVariant('debug') {
            withSourcesJar()
        }
    }
    namespace 'es.uji.geotec.backgroundsensors'
}

version = '1.2.0'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            groupId = 'io.github.geotecinit'
            artifactId = 'background-sensors'

            afterEvaluate {
                from isReleaseVersion ? components.release : components.debug
            }

            pom {
                name = 'BackgroundSensors'
                description = 'The backgroundsensors library is an Android library that allows to ' +
                        'collect data from the IMU sensors (i.e., accelerometer and gyroscope) and' +
                        ' the magnetometer (if they are present in the device). It aims to ensure' +
                        ' the data collection while the app is in background by using foregrund' +
                        ' services and wake locks.'
                url = 'https://github.com/GeoTecINIT/BackgroundSensors'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/GeoTecINIT/BackgroundSensors/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'matey'
                        name = 'Miguel Matey Sanz'
                        email = 'matey@uji.es'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/GeoTecINIT/BackgroundSensors.git'
                    developerConnection = 'scm:git:ssh://github.com/GeoTecINIT/BackgroundSensors.git'
                    url = 'https://github.com/GeoTecINIT/BackgroundSensors'
                }
            }
        }
    }

    repositories {
        maven {
            def releaseRepo = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotRepo = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'

            name = 'OSSRH'
            url = isReleaseVersion ? releaseRepo : snapshotRepo

            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }

            mavenContent {
                isReleaseVersion ? releasesOnly() : snapshotsOnly()
            }
        }
    }
}

signing {
    sign publishing.publications.mavenAndroid
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}